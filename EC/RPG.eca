require import AllCore Distr List UpdateList.

type char = int.
type password = int list.
type charSet = int list.
type policy = {length : int;
               lowercaseMin : int;
               lowercaseMax : int;
               uppercaseMin : int;
               uppercaseMax : int;
               numbersMin : int;
               numbersMax : int;
               specialMin : int;
               specialMax : int
}.


(*-------------------- Lemmas on RPG types --------------------*)
lemma char_cat2 (x:char) (set1 set2:charSet) :
  x \in (set1 ++ set2) <=>
  x \in set1 \/ x \in set2.
proof.
exact mem_cat.
qed.


lemma char_cat3 (x:char) (set1 set2 set3:charSet) :
  x \in (set1 ++ set2 ++ set3) <=>
  x \in set1 \/ x \in set2 \/ x \in set3.
proof.
smt(mem_cat).
qed.


lemma char_cat4 (x:char) (set1 set2 set3 set4:charSet) :
  x \in (set1 ++ set2 ++ set3 ++ set4) <=>
  x \in set1 \/ x \in set2 \/ x \in set3 \/ x \in set4.
proof.
smt(mem_cat).
qed.


lemma disjoint_symmetry (set1 set2:charSet) :
  (forall (x : char), x \in set1 => !(x \in set2)) <=>
  (forall (x : char), x \in set2 => !(x \in set1)).
proof.
split; move => h1 x; apply contraL; apply h1.
qed.


lemma disjoint_cat (set1 set2 set3:charSet) :
  (forall x, x \in set1 => ! (x \in set2)) =>
  (forall x, x \in set1 => ! (x \in set3)) =>
  forall x, x \in set1 => ! (x \in set2 ++ set3).
proof.
move => h1 h2 x h3.
have eq_not_mem: (!(x \in set2 ++ set3)) <=> !((mem set2 x) \/ (mem set3 x)).
  congr; apply eqboolP.
  apply char_cat2.
apply eq_not_mem.
rewrite negb_or.
split.
- apply h1. assumption.
- apply h2. assumption.
qed.


lemma charset_all (set:charSet) :
  all (mem set) set.
proof.
rewrite allP.
trivial.
qed.


lemma charset_all_concat (set1 set2:charSet) :
  all (mem (set1 ++ set2)) set1.
proof.
rewrite allP.
move => x.
elim set1.
- trivial.
- move => x0 l0 ? ?.
rewrite /mem /= in H0.
rewrite /mem /=.
case (x = x0).
- trivial.
- move => ?.
  right.
  smt. (* completar... isto Ã© trivial *)
qed.


lemma charset_concat_all (set1 set2 set3:charSet) :
  all (mem (set1 ++ set3)) set2 =>
  (forall (x : char), x \in set2 => !(x \in set3)) =>
  all (mem set1) set2.
proof.
smt.
qed.


lemma charset_disjoint_notall (set1 set2:charSet) :
  0 < size set1 =>
  0 < size set2 =>
  (forall (x : char), x \in set1 => ! (x \in set2)) =>
  !(all (mem set1) set2) /\ !(all (mem set2) set1).
proof. 
case set1.
- trivial.
- case set2.
  + trivial.
  + move => x2 l2 x1 l1 h1 h2 h3.
    split.
    * smt.
    * have aux : forall (x : char), x \in x2 :: l2 => ! (x \in x1 :: l1).
      - by apply disjoint_symmetry.
      smt.        
qed.


lemma charset_has (set:charSet) :
  0 < size set =>
  has (mem set) set.
proof.
case set.
- trivial.
- trivial.
qed.


lemma charset_has_concat (set1 set2:charSet) :
  0 < size set1 =>
  has (mem (set1 ++ set2)) set1.
proof.
smt.
qed.


lemma charset_disjoint_hasnot (set1 set2:charSet) :
  (forall (x : char), x \in set1 => ! (x \in set2)) =>
  !(has (mem set2) set1) (*/\ !(has (mem set1) set2)*).
proof.
move => h1.
rewrite hasPn.
move => x.
apply h1.
(*- rewrite hasPn.
  move => x.
  apply disjoint_symmetry.
  apply h1.*)
qed.


lemma allP_hasP (p : 'a -> bool) s :
  0 < size s =>
  all p s => has p s.
proof.
case s.
- trivial.
- move => x l H.
  rewrite /all /=.
  move => [H1 H2].
  left.
  assumption.
qed.


lemma hasPn_allPn (p : 'a -> bool) s :
  0 < size s =>
  !has p s => !all p s.
proof.
case s.
- trivial.
- move => x l H.
  rewrite hasPn.
  rewrite allP. 
  move => H1.
  apply negb_forall.
  exists (x).
  have x_in_x_l: x \in x::l.
    trivial.
  apply H1 in x_in_x_l.
  simplify.
  assumption.
qed.


(*-------------------- SetOccurrences and its lemmas --------------------*)
op setOccurrences (set:charSet) (pw:password) : int =
  with pw = []     => 0
  with pw = x :: s => if x \in set then 1 + setOccurrences set s
                                   else 0 + setOccurrences set s.

lemma setocc_empty_pw (set:charSet) :
  setOccurrences set [] = 0.
proof.
trivial.
qed.


lemma setocc_empty_set (pw:password) :
  setOccurrences [] pw = 0.
proof.
elim pw.
- trivial.
- move => x l.
  trivial.
qed.


lemma setocc_cat (set:charSet) (pw1 pw2:password) :
  setOccurrences set (pw1 ++ pw2) = setOccurrences set pw1 + setOccurrences set pw2.
proof.
elim pw1.
- rewrite cat0s.
  trivial.
- move => x l ?.
  case (x \in set).
  + move => ?.
    rewrite H0 /=.
    rewrite H.
    rewrite addzA.
    reflexivity.
  + move => ?.
    rewrite H0 /=. 
    assumption.  
qed.


lemma setocc_insert_inset (set:charSet) (pw:password) (x:char) :
  x \in set =>
  setOccurrences set (x :: pw) = (setOccurrences set pw) + 1.
proof.
move => ?.
elim pw.
- simplify. 
  rewrite H //.
- move => x0 l ?.
  rewrite /setOccurrences /=.
  rewrite H /= //.
  case (x0 \in set).  
  + move => ?.
    auto.  
  + move => ?.
    ring.
qed.


lemma setocc_insert_notinset (set:charSet) (pw:password) (x:char) :
  !(x \in set) =>
  setOccurrences set (x :: pw) = setOccurrences set pw.
proof.
move => ?.
elim pw.
- simplify.
  by rewrite H.
- move => x0 l ?.
  rewrite /setOccurrences /=.
  by rewrite H.
qed.


lemma setocc_head_cat (set:charSet) (pw:password) (x:char) :
  setOccurrences set (x :: pw) = setOccurrences set ([x] ++ pw).
proof.
by rewrite cat1s.
qed.


lemma setocc_take_drop (set:charSet) (pw:password) (n:int) :
  setOccurrences set (take n pw) + setOccurrences set (drop n pw) = setOccurrences set pw.
proof.
rewrite -setocc_cat.
rewrite cat_take_drop.
reflexivity.
qed.


lemma setocc_drop_plus1_inset (set:charSet) (pw:password) (n:int) (x:char) :
  nth x pw n \in set =>
  0 <= n =>
  n < size pw =>
  0 < size pw =>
  setOccurrences set (drop (n) pw) - 1 = setOccurrences set (drop (n + 1) pw).
proof.
case pw.
- trivial.
- move => ? ? ? ? ? ?.
  rewrite (drop_nth x).
  + smt.
  rewrite setocc_insert_inset.
  + assumption.
  ring.  
qed.

lemma setocc_drop_plus1_notinset (set:charSet) (pw:password) (n:int) (x:char) :
  !(nth x pw n \in set) =>
  0 <= n =>
  n < size pw =>
  0 < size pw =>
  setOccurrences set (drop n pw) = setOccurrences set (drop (n + 1) pw).
proof.
case pw.
- trivial.
- move => x0 l ? ? ? ?.
  rewrite (drop_nth x).
  + smt.
  rewrite setocc_insert_notinset.
  + assumption.
  reflexivity.  
qed.


lemma setocc_update_inset_inset (set:charSet) (pw:password) (x y:char) (n:int) :
  0 <= n =>
  n < size pw =>
  0 < size pw =>
  nth y pw n \in set =>
  x \in set =>
  setOccurrences set (update x pw n) = setOccurrences set pw.
proof.
move => ? ? ? ? ?.
rewrite /update. 
have n_0: !(n < 0).
+ by rewrite -lezNgt.
have size_n: !(size pw <= n).
+ by rewrite -ltzNge.
rewrite n_0.
rewrite size_n.
simplify.
rewrite setocc_cat.
rewrite setocc_insert_inset.
+ assumption.
rewrite -(setocc_drop_plus1_inset set pw n y).
+ assumption.
  assumption.
  assumption.
  assumption.
simplify.
by rewrite setocc_take_drop.
qed.


lemma setocc_update_inset_notinset (set:charSet) (pw:password) (x y:char) (n:int) :
  0 <= n =>
  n < size pw =>
  0 < size pw =>
  nth y pw n \in set =>
  !(x \in set) =>
  setOccurrences set (update x pw n) = setOccurrences set pw - 1.
proof.
move => ? ? ? ? ?.
rewrite /update. 
have n_0: !(n < 0).
+ by rewrite -lezNgt.
have size_n: !(size pw <= n).
+ by rewrite -ltzNge.
rewrite n_0.
rewrite size_n.
simplify.
rewrite setocc_cat.
rewrite setocc_insert_notinset.
+ assumption.
rewrite -(setocc_drop_plus1_inset set pw n y).
+ assumption.
  assumption.
  assumption.
  assumption.
rewrite addzA.
by rewrite setocc_take_drop.
qed.


lemma setocc_update_notinset_inset (set:charSet) (pw:password) (x y:char) (n:int) :
  0 <= n =>
  n < size pw =>
  0 < size pw =>
  !(nth y pw n \in set) =>
  x \in set =>
  setOccurrences set (update x pw n) = setOccurrences set pw + 1.
proof.
move => ? ? ? ? ?.
rewrite /update. 
have n_0: !(n < 0).
+ by rewrite -lezNgt.
have size_n: !(size pw <= n).
+ by rewrite -ltzNge.
rewrite n_0.
rewrite size_n.
simplify.
rewrite setocc_cat.
rewrite setocc_insert_inset.
+ assumption.
rewrite -(setocc_drop_plus1_notinset set pw n y).
+ assumption.
  assumption.
  assumption.
  assumption.
rewrite addzA.
by rewrite setocc_take_drop.
qed.


lemma setocc_update_notinset_notinset (set:charSet) (pw:password) (x y:char) (n:int) :
  0 <= n =>
  n < size pw =>
  0 < size pw =>
  !(nth y pw n \in set) =>
  !(x \in set) =>
  setOccurrences set (update x pw n) = setOccurrences set pw.
proof.
move => ? ? ? ? ?.
rewrite /update. 
have n_0: !(n < 0).
+ by rewrite -lezNgt.
have size_n: !(size pw <= n).
+ by rewrite -ltzNge.
rewrite n_0.
rewrite size_n.
simplify.
rewrite setocc_cat.
rewrite setocc_insert_notinset.
+ assumption.
rewrite -(setocc_drop_plus1_notinset set pw n y).
+ assumption.
  assumption.
  assumption.
  assumption.
by rewrite setocc_take_drop.
qed.


lemma nth_update (set:charSet) (pw:password) (i j:int) (x:char) :
  nth x pw i = nth x (update (nth x pw i) pw j) i.
proof.
rewrite /update.
case (j < 0).
- trivial.
- case (size pw <= j).
  + trivial.
  + move => ? ?.
    smt. (*completar nao parece dificil*)
qed.

lemma setocc_swap (set:charSet) (pw:password) (x:char) (i j:int) :
  0 < i =>
  i <= size pw =>
  j < i =>
  0 <= j =>
setOccurrences set (update (nth x pw (i - 1)) (update (nth x pw j) pw (i - 1)) j) =
setOccurrences set pw.
proof.
move => ? ? ? ?. 
case ((nth x pw (i - 1)) \in set).
- case ((nth x pw j) \in set).
  + move => ? ?.
    rewrite (setocc_update_inset_inset set (update (nth x pw j) pw (i - 1)) (nth x pw (i-1)) x j).
    * assumption.
    * smt.
    * smt. 
    * smt.
    * smt.
    rewrite -(setocc_update_inset_inset set pw (nth x pw j) x (i-1)).
    * smt.
    * smt.
    * smt.
    * assumption.
    * assumption.
    reflexivity.    
  + move => ? ?.
    rewrite (setocc_update_notinset_inset set (update (nth x pw j) pw (i - 1)) (nth x pw (i-1)) x j).
    * assumption.
    * smt.
    * smt. 
    * smt.
    * smt.
    rewrite (setocc_update_inset_notinset set pw (nth x pw j) x (i-1) ).
    * smt.
    * smt.
    * smt.
    * assumption.
    * assumption.
    ring.
- case ((nth x pw j) \in set).
  + move => ? ?.
    rewrite (setocc_update_inset_notinset set (update (nth x pw j) pw (i - 1)) (nth x pw (i-1)) x j).
    * assumption.
    * smt.
    * smt. 
    * smt.
    * smt.
    rewrite (setocc_update_notinset_inset set pw (nth x pw j) x (i-1) ).
    * smt.
    * smt.
    * smt.
    * assumption.
    * assumption.
    reflexivity. 
  + move => ? ?.
    rewrite (setocc_update_notinset_notinset set (update (nth x pw j) pw (i - 1)) (nth x pw (i-1)) x j).
    * assumption.
    * smt.
    * smt. 
    * smt.
    * smt.
    rewrite (setocc_update_notinset_notinset set pw (nth x pw j) x (i-1)).
    * smt.
    * smt.
    * smt.
    * assumption.
    * assumption.
    reflexivity. 
qed.


lemma setocc_eq (pwA pwB:password) (set:charSet):
  pwA = pwB =>
  setOccurrences set pwA = setOccurrences set pwB.
proof.
trivial.
qed.


op satisfiesMin (min:int) (set:charSet) (pw:password) : bool =
  min <= setOccurrences set pw.

op satisfiesMax (max:int) (set:charSet) (pw:password) : bool =
  setOccurrences set pw <= max.




(* Abstract type of a RPG *)
module type RPG_T = { 
  proc generate_password(policy:policy) : password
  proc get_lowercase() : charSet
  proc get_uppercase() : charSet
  proc get_numbers() : charSet
  proc get_special() : charSet
}.


(* Correctness definition for a RPG*)
module Correctness(RPG : RPG_T) = {


  proc main(policy:policy) : bool = {
  
    var pw : password;
    var lowercaseSet, uppercaseSet, numbersSet, specialSet : charSet;
    var lowercaseOcc, uppercaseOcc, numbersOcc, specialOcc : int;
    var satisfied : bool;

    if(policy.`length <= 200 /\
       0 < policy.`length /\ 
       0 <= policy.`lowercaseMin /\
       0 <= policy.`uppercaseMin /\
       0 <= policy.`numbersMin /\
       0 <= policy.`specialMin /\
       0 <= policy.`lowercaseMax /\
       0 <= policy.`uppercaseMax /\
       0 <= policy.`numbersMax /\
       0 <= policy.`specialMax /\
       policy.`lowercaseMin <= policy.`lowercaseMax /\
       policy.`uppercaseMin <= policy.`uppercaseMax /\
       policy.`numbersMin <= policy.`numbersMax /\
       policy.`specialMin <= policy.`specialMax /\
       policy.`lowercaseMin + policy.`uppercaseMin + policy.`numbersMin + policy.`specialMin
         <= policy.`length /\
       policy.`length <=
         policy.`lowercaseMax + policy.`uppercaseMax + policy.`numbersMax + policy.`specialMax) {

    lowercaseSet <- RPG.get_lowercase();
    uppercaseSet <- RPG.get_uppercase();
    numbersSet <- RPG.get_numbers();
    specialSet <- RPG.get_special();

    pw <@ RPG.generate_password(policy);
    
    satisfied <- size pw = policy.`length /\
                 satisfiesMin policy.`lowercaseMin lowercaseSet pw /\
                 satisfiesMax policy.`lowercaseMax lowercaseSet pw /\
                 satisfiesMin policy.`uppercaseMin uppercaseSet pw /\
                 satisfiesMax policy.`uppercaseMax uppercaseSet pw /\
                 satisfiesMin policy.`numbersMin numbersSet pw /\
                 satisfiesMax policy.`numbersMax numbersSet pw /\
                 satisfiesMin policy.`specialMin specialSet pw /\
                 satisfiesMax policy.`specialMax specialSet pw;
    }

    else {
      satisfied <- true;
    }


    
    return satisfied;

   }

}.
